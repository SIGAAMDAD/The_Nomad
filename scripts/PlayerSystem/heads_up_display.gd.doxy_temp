#
# ===========================================================================
# Copyright (C) 2023-2025 Noah Van Til
#
# This file is part of The Nomad source code.
#
# The Nomad source code is free software; you can redistribute it
# and/or modify it under the terms of the GNU Affero General Public License as
# published by the Free Software Foundation; either version 2 of the License,
# or (at your option) any later version.
#
# The Nomad source code is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
# GNU Affero General Public License for more details.
#
# You should have received a copy of the GNU Affero General Public License
# along with The Nomad source code; if not, write to the Free Software
# Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA
# ===========================================================================
#

class_name HeadsUpDisplay extends CanvasLayer

@export var _owner: CharacterBody2D

const DEFAULT_COLOR: Color = Color( 1.0, 1.0, 1.0, 1.0 )
const _hidden_color: Color = Color( 0.0, 0.0, 0.0, 0.0 )
const _arm_used_color: Color = Color( 0.0, 0.0, 1.0, 1.0 )
const _arm_unused_color: Color = Color( 0.20, 0.20, 0.20, 1.0 )

var _fadeout_tween: Tween
var _fadein_tween: Tween

@onready var _healthbar: HealthBar = $MainHUD/HealthBar
@onready var _ragebar: RageBar = $MainHUD/RageBar
@onready var _dash_status: DashStatus = $MainHUD/DashStatus

@onready var _emote_menu: Control = $MainHUD/EmoteMenu

@onready var _announcement_container: MarginContainer = $MainHUD/AnnouncementLabel
@onready var _announcement_background: TextureRect = $MainHUD/AnnouncementLabel/TextureRect
@onready var _announcement_text: Label = $MainHUD/AnnouncementLabel/TextureRect/Label
@onready var _announcement_timer: Timer = $MainHUD/AnnouncementLabel/Timer

@onready var _checkpoint_interactor: MarginContainer = $CheckpointContainer
@onready var _jump_interactor: MarginContainer = $JumpContainer
@onready var _door_interactor: MarginContainer = $DoorContainer

var _current_interactor: MarginContainer

# eagles peak interaction
@onready var _jump_yes_button: Button = $JumpContainer/JumpQueryContainer/VBoxContainer/YesButton
@onready var _jump_no_button: Button = $JumpContainer/JumpQueryContainer/VBoxContainer/NoButton
@onready var _jump_view_image: TextureRect = $JumpContainer/JumpQueryContainer/Background
@onready var _jump_music: AudioStreamPlayer = $JumpContainer/Theme
var _on_yes_pressed: Callable
var _on_no_pressed: Callable

@onready var _left_arm_indicator: TextureRect = $MainHUD/WeaponStatus/MarginContainer/VBoxContainer/ArmUsage/LeftArmIndicator
@onready var _right_arm_indicator: TextureRect = $MainHUD/WeaponStatus/MarginContainer/VBoxContainer/ArmUsage/RightArmIndicator

@onready var _reflex_overlay: TextureRect = $MainHUD/Overlays/ReflexModeOverlay
@onready var _dash_overlay: TextureRect = $MainHUD/Overlays/DashOverlay
@onready var _parry_overlay: TextureRect = $MainHUD/Overlays/ParryOverlay

@onready var _save_timer: Timer = $MainHUD/SaveSpinner/SaveTimer
@onready var _save_spinner: Spinner = $MainHUD/SaveSpinner/SaveSpinner

var _weapon_data: Node = null
var _weapon_status_timer: Timer = Timer.new()

@onready var _weapon_status: TextureRect = $MainHUD/WeaponStatus
@onready var _weapon_mode_bladed: TextureRect = $MainHUD/WeaponStatus/MarginContainer/VBoxContainer/HBoxContainer/MarginContainer/StatusContainer/StatusBladed
@onready var _weapon_mode_blunt: TextureRect = $MainHUD/WeaponStatus/MarginContainer/VBoxContainer/HBoxContainer/MarginContainer/StatusContainer/StatusBlunt
@onready var _weapon_mode_firearm: TextureRect = $MainHUD/WeaponStatus/MarginContainer/VBoxContainer/HBoxContainer/MarginContainer/StatusContainer/StatusFirearm

@onready var _weapon_status_firearm: VBoxContainer = $MainHUD/WeaponStatus/MarginContainer/VBoxContainer/HBoxContainer/FireArmStatus
@onready var _weapon_status_melee: VBoxContainer = $MainHUD/WeaponStatus/MarginContainer/VBoxContainer/HBoxContainer/MeleeStatus
@onready var _weapon_status_melee_icon: TextureRect = $MainHUD/WeaponStatus/MarginContainer/VBoxContainer/HBoxContainer/MeleeStatus/WeaponIcon
@onready var _weapon_status_firearm_icon: TextureRect = $MainHUD/WeaponStatus/MarginContainer/VBoxContainer/HBoxContainer/FireArmStatus/WeaponIcon
@onready var _weapon_status_bullet_count: Label = $MainHUD/WeaponStatus/MarginContainer/VBoxContainer/HBoxContainer/FireArmStatus/AmmunitionContainer/BulletCountLabel
@onready var _weapon_status_bullet_reserve: Label = $MainHUD/WeaponStatus/MarginContainer/VBoxContainer/HBoxContainer/FireArmStatus/AmmunitionContainer/BulletReserveLabel

@onready var _interaction_data

@onready var _open_door_button: Button = $DoorContainer/MarginContainer/OpenButton
@onready var _use_door_button: Button = $DoorContainer/MarginContainer/UseButton

@onready var _boss_health_bar: Control = $MainHUD/BossHealthBar

@onready var _world_time_year: Label = $MainHUD/WorldTimeContainer/VBoxContainer/HBoxContainer/YearLabel
@onready var _world_time_month: Label = $MainHUD/WorldTimeContainer/VBoxContainer/HBoxContainer/MonthLabel
@onready var _world_time_day: Label = $MainHUD/WorldTimeContainer/VBoxContainer/HBoxContainer2/DayLabel
@onready var _world_time_hour: Label = $MainHUD/WorldTimeContainer/VBoxContainer/HBoxContainer2/HourLabel
@onready var _world_time_minute: Label = $MainHUD/WorldTimeContainer/VBoxContainer/HBoxContainer2/MinuteLabel

@onready var _location_label: Label = $MainHUD/LocationLabel
var _location_status_timer:Timer = Timer.new()

@onready var _objective_label: Label = $MainHUD/ObjectiveLabel
var _objective_status_timer: Timer = Timer.new()

var _hud_preset: VisibilityPreset
var _status_icons: Dictionary[String, TextureRect]

@onready var _hellbreaker_overlay: Control = $MainHUD/HellbreakerOverlay

enum HandsUsed {
	Left,
	Right,
	Both
};

enum WeaponProperties {
	IsOneHanded			= 0b01000000,
	IsTwoHanded			= 0b00100000,
	IsBladed			= 0b00000001,
	IsBlunt				= 0b00000010,
	IsFirearm			= 0b00001000,

	OneHandedBlade		= IsOneHanded | IsBladed,
	OneHandedBlunt		= IsOneHanded | IsBlunt,
	OneHandedFirearm	= IsOneHanded | IsFirearm,

	TwoHandedBlade		= IsTwoHanded | IsBladed,
	TwoHandedBlunt		= IsTwoHanded | IsBlunt,
	TwoHandedFirearm	= IsTwoHanded | IsFirearm,

	SpawnsObject		= 0b10000000,

	None				= 0b00000000
};

enum InteractionType {
	Checkpoint,
	TreasureChest,
	Tutorial,
	Note,
	Door,

	EaglesPeak,

	Count
};

enum VisibilityPreset {
	# everything
	Full,

	# only essential information
	Partial,

	# hidden
	Expert
};

enum DoorState {
	Locked,
	Unlocked,

	Count
};

func GetCurrentCheckpoint() -> Area2D:
	return _checkpoint_interactor.GetCurrentCheckpoint()

func GetHealthBar() -> ProgressBar:
	return _healthbar

func GetRageBar() -> ProgressBar:
	return _ragebar

func GetParryOverlay() -> TextureRect:
	return _parry_overlay

func SetRage( rage: float ) -> void:
	_ragebar.value = rage

func SaveStart() -> void:
	_save_spinner.set_process( true )
	_save_spinner.show()

func SaveEnd() -> void:
	_save_timer.start()

func _on_save_timer_timeout() -> void:
	_save_spinner.hide()
	_save_spinner.set_process( false )

func _on_use_door_transition_finished() -> void:
	get_node( "/root/TransitionScreen" ).disconnect( "transition_finished", _on_use_door_transition_finished )
	
	_owner.global_position = _interaction_data.GetDestination().global_position
	_owner.BlockInput( false )

func _on_use_door_button_pressed() -> void:
	_owner.BlockInput( true )
	get_node( "/root/TransitionScreen" ).connect( "transition_finished", _on_use_door_transition_finished )
	get_node( "/root/TransitionScreen" ).call( "transition" )
	_owner.PlaySound( null, load( "res://sounds/env/open_door_" + var_to_str( randi_range( 0, 2 ) ) + ".ogg" ) )
	
	_door_interactor.hide()

func _on_hands_status_updated( handsUsed: int ) -> void:
	if _hud_preset == VisibilityPreset.Expert:
		return
	_weapon_status.modulate = _default_color
	
	match handsUsed:
		HandsUsed.Left:
			_left_arm_indicator.modulate = _arm_used_color
			_right_arm_indicator.modulate = _arm_unused_color
		HandsUsed.Right:
			_left_arm_indicator.modulate = _arm_unused_color
			_right_arm_indicator.modulate = _arm_used_color
		HandsUsed.Both:
			_left_arm_indicator.modulate = _arm_used_color
			_right_arm_indicator.modulate = _arm_used_color

func _on_weapon_status_updated( source: Node, properties: int ) -> void:
	if _hud_preset == VisibilityPreset.Expert:
		return
	if source == null:
		_weapon_data = null
		
		_weapon_status.modulate = _hidden_color
		return
	if source != _weapon_data:
		return
	
	_weapon_status.modulate = _default_color
	
	_weapon_mode_bladed.visible = source.LastUsedMode & WeaponProperties.IsBladed
	_weapon_mode_blunt.visible = source.LastUsedMode & WeaponProperties.IsBlunt
	
	if source.LastUsedMode & WeaponProperties.IsFirearm:
		_weapon_mode_firearm.show()
		_weapon_status_bullet_count.text = var_to_str( source.BulletsLeft )
		if source.Reserve != null:
			_weapon_status_bullet_reserve.text = var_to_str( source.Reserve.Amount )
	else:
		_weapon_mode_firearm.hide()

func _on_announcement_timer_timeout() -> void:
	create_tween().tween_property( _announcement_background.material, "shader_parameter/alpha", 0.0, 1.0 )
	create_tween().tween_property( _announcement_text, "modulate", _hidden_color, 1.0 )

func _fade_ui_element( element: Control, duration: float, timer: Timer ) -> void:
	create_tween().tween_property( element, "modulate", _hidden_color, duration )
	timer.process_mode = PROCESS_MODE_DISABLED

func _on_location_changed( location: Area2D ) -> void:
	if _hud_preset > VisibilityPreset.Full:
		return

	_location_status_timer.process_mode = PROCESS_MODE_PAUSABLE
	
	_location_label.text = location.GetAreaName()
	
	var _tweener: Tween = create_tween()
	_tweener.tween_property( _location_label, "modulate", _default_color, 1.5 )
	_tweener.connect( "finished", func(): _location_status_timer.start() )

func _on_weapon_reloaded( source: Node ) -> void:
	if _hud_preset == VisibilityPreset.Expert:
		return
	
	_weapon_status.modulate = _default_color
	
	_weapon_status_bullet_count.text = var_to_str( source.BulletsLeft )
	_weapon_status_bullet_reserve.text = var_to_str( source.Reserve.Amount ) if source.Reserve != null else "0"

func _on_weapon_used( source: Node ) -> void:
	if _hud_preset == VisibilityPreset.Expert:
		return
	
	_weapon_status.modulate = _default_color
	
	if source.LastUsedMode & WeaponProperties.IsFirearm:
		_weapon_status_bullet_count.text = var_to_str( source.BulletsLeft )

func _on_switched_weapon( weapon: Node ) -> void:
	if _hud_preset == VisibilityPreset.Expert:
		return
	if _weapon_data != weapon && _weapon_data != null:
		if _weapon_data.is_connected( "Reloaded", _on_weapon_reloaded ):
			_weapon_data.disconnect( "Reloaded", _on_weapon_reloaded )
		if _weapon_data.is_connected( "Used", _on_weapon_used ):
			_weapon_data.disconnect( "Used", _on_weapon_used )
	
	if weapon == null:
		_weapon_status.modulate = _hidden_color
		_weapon_status.process_mode = PROCESS_MODE_DISABLED
		return
	else:
		_weapon_status.process_mode = PROCESS_MODE_PAUSABLE
	
	_weapon_status.modulate = _default_color
	
	if weapon.LastUsedMode & WeaponProperties.IsFirearm:
		_weapon_status_firearm.show()
		_weapon_status_melee.hide()
		
		_weapon_status_firearm_icon.texture = weapon.Icon
		
		_weapon_status_bullet_count.text = var_to_str( weapon.BulletsLeft )
		_weapon_status_bullet_reserve.text = var_to_str( weapon.Reserve.Amount ) if weapon.Reserve != null else "0"
	else:
		_weapon_status_firearm.hide()
		_weapon_status_melee.show()
		_weapon_status_melee_icon.texture = weapon.Icon
	
	_weapon_data = weapon
	_weapon_data.connect( "Reloaded", _on_weapon_reloaded )
	_weapon_data.connect( "Used", _on_weapon_used )

func _on_health_changed( health: float ) -> void:
	if _hud_preset == VisibilityPreset.Expert:
		return
	
	_healthbar.SetHealth( health )
	
	get_node( "MainHUD/Overlays/InjuredOverlay/MediumHealth" ).visible = health < 60.0
	get_node( "MainHUD/Overlays/InjuredOverlay/LowHealth" ).visible = health < 30.0
	get_node( "MainHUD/Overlays/InjuredOverlay/CriticalHealth" ).visible = health < 15.0

func EndParry() -> void:
	_parry_overlay.hide()

func _on_dialogue_interaction() -> void:
	get_node( "MainHUD" ).visible = !get_node( "DialogueContainer" ).visible

func _on_settings_changed() -> void:
	Console.PrintLine( "Updating hud visibility preset..." )

	_hud_preset = SettingsData.GetHUDPreset()
	match _hud_preset:
		VisibilityPreset.Full:
			# show everything
			_location_label.show()
			_objective_label.show()
			_dash_status.show()
			_healthbar.show()
			_ragebar.show()
			_weapon_data.show()
		VisibilityPreset.Partial:
			# only show stuff that's actually important
			_location_label.hide()
			_objective_label.hide()
			_dash_status.hide()

			_healthbar.show()
			_ragebar.show()
			_weapon_data.show()
		VisibilityPreset.Expert:
			# show nothing, let the player determine what their status is
			_healthbar.hide()
			_ragebar.hide()
			_weapon_data.hide()
			_location_label.hide()
			_objective_label.hide()
			_dash_status.hide()

func _ready() -> void:
	_weapon_status_timer.name = "WeaponStatusTimer"
	_weapon_status_timer.wait_time = 5.90
	_weapon_status_timer.one_shot = true
	_weapon_status_timer.process_mode = PROCESS_MODE_DISABLED
	_weapon_status_timer.connect( "timeout", func(): _fade_ui_element( _weapon_status, 1.5, _weapon_status_timer ) )
	add_child( _weapon_status_timer )
	
	_location_status_timer.name = "LocationStatusTimer"
	_location_status_timer.wait_time = 5.90
	_location_status_timer.one_shot = true
	_location_status_timer.process_mode = PROCESS_MODE_DISABLED
	_location_status_timer.connect( "timeout", func(): _fade_ui_element( _location_label, 2.5, _location_status_timer ) )
	add_child( _location_status_timer )
	
	_objective_status_timer.name = "ObjectiveStatusTimer"
	_objective_status_timer.wait_time = 5.90
	_objective_status_timer.one_shot = true
	_objective_status_timer.process_mode = PROCESS_MODE_DISABLED
	_objective_status_timer.connect( "timeout", func(): _fade_ui_element( _objective_label, 2.5, _objective_status_timer ) )
	add_child( _objective_status_timer )

	get_node( "DialogueContainer" ).connect( "visibility_changed", _on_dialogue_interaction )
	
	_owner.connect( "DashStart", _dash_overlay.show )
	_owner.connect( "DashEnd", _dash_overlay.hide )
	_owner.connect( "BulletTimeStart", _reflex_overlay.show )
	_owner.connect( "BulletTimeEnd", _reflex_overlay.hide )
	_owner.connect( "HideInteraction", HideInteraction )
	_owner.connect( "ShowInteraction", func( interaction: Area2D ): ShowInteraction( interaction ) )
	_owner.connect( "ParrySuccess", _parry_overlay.show )
	_owner.connect( "DashBurnoutChanged", func( burnout: float ): if _hud_preset < VisibilityPreset.Expert: _dash_status.DashBurnout = burnout )
	_owner.connect( "HealthChanged", _on_health_changed )
	_owner.connect( "RageChanged", func( rage: float ): if _hud_preset < VisibilityPreset.Expert: _ragebar.Rage = rage )
	_owner.connect( "LocationChanged", _on_location_changed )
	_owner.connect( "Damaged", func( source: CharacterBody2D, target: CharacterBody2D, amount: float ): if _hud_preset < VisibilityPreset.Expert: _healthbar.SetHealth( target.GetHealth() ) )
	_owner.connect( "SwitchedWeapon", _on_switched_weapon )
	_owner.connect( "HandsStatusUpdated", _on_hands_status_updated )
	_owner.connect( "WeaponStatusUpdated", _on_weapon_status_updated )

func _on_jump_audio_tween_fade_out_finished() -> void:
	_jump_music.stop()
	_jump_music.volume_db = 0.0

func ShowInteraction( item: Area2D ) -> void:
	match item.GetInteractionType():
		InteractionType.Checkpoint:
			_checkpoint_interactor.BeginInteraction( item )
			_current_interactor = _checkpoint_interactor
		InteractionType.Door:
			_current_interactor = _door_interactor
		InteractionType.EaglesPeak:
			_current_interactor = _jump_interactor
	
	if _current_interactor != null:
		_current_interactor.show()
	else:
		return
	
	Input.set_custom_mouse_cursor( load( "res://cursor_n.png" ) )
	
	_interaction_data = item
	
	if _current_interactor == _door_interactor:
		match item.GetState():
			DoorState.Locked:
				_open_door_button.show()
				_use_door_button.hide()
			DoorState.Unlocked:
				_open_door_button.hide()
				_use_door_button.show()
	elif _current_interactor == _jump_interactor:
		_on_yes_pressed = item.OnYesButtonPressed
		_on_no_pressed = item.OnNoButtonPressed
		
		_jump_yes_button.connect( "pressed", _on_yes_pressed )
		_jump_no_button.connect( "pressed", _on_no_pressed )
		
		_jump_view_image.texture = item.GetViewImage()
		_jump_music.stream = item.GetMusic()
		
		_jump_music.play()

func HideInteraction() -> void:
	if _current_interactor == _checkpoint_interactor:
		_checkpoint_interactor.EndInteraction()
	elif _current_interactor == _jump_interactor:
		var _audio_tween: Tween = create_tween()
		_audio_tween.tween_property( _jump_music, "volume_db", -20.0, 1.5 )
		_audio_tween.connect( "finished", _on_jump_audio_tween_fade_out_finished )
		
		_jump_yes_button.disconnect( "pressed", _on_yes_pressed )
		_jump_no_button.disconnect( "pressed", _on_no_pressed )
	
	if _current_interactor != null:
		_current_interactor.hide()
	
	_current_interactor = null
	Input.set_custom_mouse_cursor( load( "res://textures/hud/crosshairs/crosshairi.tga" ), Input.CURSOR_ARROW )

func ShowAnnouncment( text: String ) -> void:
	_announcement_container.show()
	
	_announcement_timer.start()
	
	_announcement_background.material.set( "shader_parameter/alpha", 0.0 )
	_announcement_text.text = text
	
	_announcement_timer.start()
	
	create_tween().tween_property( _announcement_background.material, "shader_parameter/alpha", 0.90, 0.90 )
	create_tween().tween_property( _announcement_text, "modulate", Color( 1.0, 1.0, 1.0, 1.0 ), 0.90 )

func _unhandled_input( event: InputEvent ) -> void:
	if Input.is_action_just_pressed( "open_emote_menu" ):
		_emote_menu.show()
		_emote_menu.set_process_input( true )
		_emote_menu.process_mode = PROCESS_MODE_ALWAYS
	if Input.is_action_just_released( "open_emote_menu" ):
		_emote_menu.hide()
		_emote_menu.set_process_input( false )
		_emote_menu.process_mode = PROCESS_MODE_DISABLED
